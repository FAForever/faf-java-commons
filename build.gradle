buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }

  dependencies {
    classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3")
    classpath("com.adarshr:gradle-test-logger-plugin:4.0.0")
  }
}

subprojects {

  apply plugin: 'maven-publish'
  apply plugin: 'java-library'
  apply plugin: 'com.github.kt3k.coveralls'
  apply plugin: 'com.adarshr.test-logger'

  group = 'com.faforever.commons'
  version = '1.0-SNAPSHOT'

  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21

  repositories {
    mavenCentral()
  }

  publishing {
    publications {
      maven(MavenPublication) {
        from components.java
      }
    }
  }

  dependencies {
    compileOnly(libs.jackson.databind)
    compileOnly(libs.slf4j.api)

    testImplementation(libs.junit.jupiter)
    testImplementation(libs.hamcrest.core)
    testImplementation(libs.mockito.core)
    testImplementation(libs.mockito.junit.jupiter)
  }

  test {
    useJUnitPlatform()

    testLogging {
      exceptionFormat = 'full'
    }
  }

}
